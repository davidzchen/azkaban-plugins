apply plugin: 'lesscss'
apply plugin: 'dustjs'

configurations {
  all {
    transitive = false
  }
}

dependencies {
  compile(project(':hadoopsecuritymanager'))
  compile(project(':jobtype'))
  compile('javax.servlet:servlet-api:2.5')
  compile('org.apache.pig:pig:0.12.0')
  compile('com.linkedin.azkaban:azkaban:2.5.0')
  compile('log4j:log4j:1.2.16')

  testCompile('junit:junit:4.11')
}

lesscss {
  source = fileTree('src/main/less') {
    include 'pigvisualizer.less'
  }
  dest = 'build/web/css'
}

dustjs {
  source = fileTree('src/main/tl')
  dest = 'build/web/js'
}

task copyWeb(type: Copy) {
  from('src/web')
  into('build/package/web')
}

task copyLess(type: Copy, dependsOn: ['lesscss', 'copyWeb']) {
  from('build/less')
  into('build/package/web/css')
}

task copyDust(type: Copy, dependsOn: ['dustjs', 'copyWeb']) {
  from('build/dust')
  into('build/package/web/js')
}

task copyLibs(type: Copy, dependsOn: 'build') {
  from('build/libs')
  into('build/package/lib')
}

task copyDeps(type: Copy, dependsOn: 'build') {
  from configurations.compile
  into('build/package/lib')
}

task copy(dependsOn: ['copyDust', 'copyLess', 'copyLibs', 'copyDeps']) {
}

distributions {
  main {
    baseName = 'azkaban-pig-visualizer'
    contents {
      from { files('build/package') }
    }
  }
}

distTar.dependsOn 'copy'
distTar.extension = 'tar.gz'
distZip.dependsOn 'copy'
