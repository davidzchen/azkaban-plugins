
def cmdCaller = { commandln ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine commandln
    standardOutput stdout
  }
  return stdout.toString().trim()
}

def getVersionName = { ->
  return cmdCaller(['git', 'describe', '--tags', '--abbrev=0'])
}

configure(allprojects) { project ->
  repositories {
    mavenCentral()
    maven { url 'http://repo.springsource.org/plugins-release' }
    mavenLocal()
  }

  apply plugin: 'java'
  apply plugin: 'eclipse'

  version = getVersionName()
  archivesBaseName = 'azkaban'
  check.dependsOn.remove(test)

  configurations {
    all {
      transitive = false
    }
    compile {
      description = 'compile classpath'
    }
    test {
      extendsFrom compile
    }
  }

  dependencies {
    compile('com.linkedin.azkaban:azkaban:2.5.0')
    testCompile('junit:junit:4.11')
  }
}

project('hadoopsecuritymanager') {
  dependencies {
    compile('org.apache.hadoop:hadoop-core:1.2.1')
    compile('org.apache.hive:hive-common:0.10.0')
    compile('org.apache.hive:hive-metastore:0.10.0')
    compile('org.apache.thrift:libthrift:0.9.0')
  }
}

project('hdfsviewer') {
  dependencies {
    compile(project(':hadoopsecuritymanager'))
    compile('org.apache.hadoop:hadoop-core:1.2.1')
    compile('org.apache.avro:avro:1.7.4')
    compile('org.mogodb:mongo-java-driver:2.10.1')
    compile('com.twitter:parquet-avro:1.3.2')
    compile('com.twitter:parquet-hadoop-bundle:1.3.2')
    compile('org.xerial.snappy:snappy-java:1.0.5')
    compile('voldemort:voldemort:0.96')
  }
}

project('javaviewer') {
  dependencies {
    compile(project(':hadoopsecuritymanager'))
    compile('javax.servlet:servlet-api:2.5')
  }
}

project('jobsummary') {
  dependencies {
    compile(project(':hadoopsecuritymanager'))
    compile('javax.servlet:servlet-api:2.5')
  }
}

project('pigvisualizer') {
  dependencies {
    compile(project(':hadoopsecuritymanager'))
    compile('javax.servlet:servlet-api:2.5')
    compile('org.apache.pig:pig:0.12.0')
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.11'
}
