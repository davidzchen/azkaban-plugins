buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
    classpath 'com.linkedin:gradle-dustjs-plugin:1.0.0'
  }
}

/**
 * Helper that calls a command and returns the output.
 */
def cmdCaller = { commandln ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine commandln
    standardOutput stdout
  }
  return stdout.toString().trim()
}

allprojects {
  repositories {
    mavenCentral()
    maven {
      url 'http://repo.springsource.org/plugins-release'
    }
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'distribution'
  apply plugin: 'eclipse'

  /**
   * Gets the version name from the latest Git tag
   */
  task createVersionFile() << {
    String gitCommitHash = cmdCaller(['git', 'rev-parse', 'HEAD']);
    String gitRepo = cmdCaller(['git', 'config', '--get', 'remote.origin.url']);
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd hh:mm zzz')

    String versionStr = version + '\n' +
        gitCommitHash + '\n' +
        gitRepo + '\n' +
        formattedDate + '\n'

    File versionFile = file('build/package/version.file')
    versionFile.parentFile.mkdirs()
    versionFile.write(versionStr)
  }
}

/**
 * Gradle wrapper.
 */
task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
}
